import org.gradle.internal.os.OperatingSystem

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.6.10'
}

group 'com.bobbyjap'
version '1.0-SNAPSHOT'

def kotlinVersion = '1.6.10'
def lwjglVersion = '2.9.3'

def lwjglNatives
switch (OperatingSystem.current()) {
    case OperatingSystem.LINUX:
        lwjglNatives = "linux"
        break
    case OperatingSystem.MAC_OS:
        lwjglNatives = "macosx"
        break
    case OperatingSystem.SOLARIS:
        lwjglNatives = "solaris"
        break
    case OperatingSystem.WINDOWS:
        lwjglNatives = "windows"
        break
    default:
        throw new GradleException('Current operating system not supported')
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    implementation "org.lwjgl.lwjgl:lwjgl:${lwjglVersion}"
    implementation "org.lwjgl.lwjgl:lwjgl_util:${lwjglVersion}"
}

repositories {
    mavenCentral()
}

task execute(type: JavaExec) {
    systemProperty('java.library.path', "$rootDir/libs/natives/${lwjglNatives}")

    classpath(sourceSets.main.runtimeClasspath)
    main('enginetester.MainGameLoopKt')
}